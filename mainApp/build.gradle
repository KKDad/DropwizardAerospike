plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'com.coditory.integration-test' version '1.1.11'
    id "com.bmuschko.docker-remote-api" version "6.7.0"
}

repositories {
    jcenter()
    mavenCentral()
}

apply plugin: "java"
apply plugin: "application"
apply plugin: "com.coditory.integration-test"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "com.bmuschko.docker-remote-api"

ext {
    // Organized alphabetically - Please keep it that way!
    aeroSpikeVersion = "ce-5.6.0.4"
    areoSpikeJdbcVersion = "1.2.0"
    dropwizardMetricsServletsVersion = '4.1.12'
    dropwizardSwaggerVersion = '2.0.12-1'
    dropwizardVersion = '2.0.12'
    gsonVersion = '2.8.6'
    hamcrestVersion = '1.3'
    junitJupiterVersion = '5.5.1'
    log4jVersion="2.13.3"
    mockitoVersion = '2.23.0'
    presidoVersion = "latest"
}

dependencies {
    implementation "io.dropwizard:dropwizard-core:$dropwizardVersion"
    implementation "io.dropwizard:dropwizard-jdbi3:$dropwizardVersion"
    implementation "com.aerospike:aerospike-jdbc:$areoSpikeJdbcVersion"

    runtime 'org.apache.logging.log4j:log4j-core:2.14.1'

    testImplementation "io.dropwizard:dropwizard-testing:$dropwizardVersion"
    testImplementation "com.google.code.gson:gson:$gsonVersion"

    // Use JUnit test framework
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitJupiterVersion"

    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.hamcrest:hamcrest-all:$hamcrestVersion"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"
}

// Load our version number from the version.properties file.
def propFile = file('../version.properties')
if (propFile.canRead()){
    Properties props = new Properties()
    props.load(new FileInputStream(propFile))
    if (props!=null && props.containsKey('SEARCH_VERSION'))
        version = props['SEARCH_VERSION']
    else
        throw new GradleException('version.properties cannot be loaded or is missing key: SEARCH_VERSION')
} else {
    throw new GradleException('version.properties cannot be read')
}
logger.warn "Building version: " + version
group = 'com.ceridian.search'

sourceCompatibility = '1.13'
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    useJUnitPlatform()
}

/**************************************************************************************************************
                Testing Helpers
                    - Start/Stop AeroSpike
                    - Start/Stop Presidio-Analyzer
                    - Start/Stop Presidio-Anonymizer
/**************************************************************************************************************/
import com.bmuschko.gradle.docker.tasks.container.DockerCreateContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStartContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStopContainer
import com.bmuschko.gradle.docker.tasks.image.DockerPullImage

static boolean get_port_status(int port) {
    Socket s = null;
    try {
        s = new Socket("127.0.0.1", port);
        return true;
    } catch (Exception e) {
        return false;
    }
    finally {
        if(s != null)
            try { s.close(); }
            catch(Exception ignored){}
    }
}


task pullImageAeroSpike(type: DockerPullImage) {
    image = "aerospike:$aeroSpikeVersion"
}
task pullImagePresidioAnalyzer(type: DockerPullImage) {
    image = "mcr.microsoft.com/presidio-analyzer:$presidoVersion"
}
task pullImagePresidioAnonymizer(type: DockerPullImage) {
    image = "mcr.microsoft.com/presidio-anonymizer:$presidoVersion"
}

task createContainerAeroSpike(type: DockerCreateContainer) {
    dependsOn pullImageAeroSpike
    targetImageId "aerospike:$aeroSpikeVersion"
    containerName.set("aerospike")
    hostConfig.portBindings.add("3000:3000")
    hostConfig.portBindings.add("3001:3002")
    hostConfig.portBindings.add("3002:3002")
    hostConfig.autoRemove.set(true)
    withEnvVar("NAMESPACE", "test")
}
task createContainerPresidioAnalyzer(type: DockerCreateContainer) {
    dependsOn pullImagePresidioAnalyzer
    targetImageId "mcr.microsoft.com/presidio-analyzer:$presidoVersion"
    containerName.set("presidio-analyzer")
    hostConfig.portBindings.add( "5001:3000")
    exposePorts('tcp', [3000])
    hostConfig.autoRemove.set(true)
}
task createContainerPresidioAnonymizer(type: DockerCreateContainer) {
    dependsOn pullImagePresidioAnonymizer
    targetImageId "mcr.microsoft.com/presidio-anonymizer:$presidoVersion"
    containerName.set("presidio-anonymizer")
    hostConfig.portBindings.add( "5002:3000")
    exposePorts('tcp', [3000])
    hostConfig.autoRemove.set(true)
}

task start_aerospike(type: DockerStartContainer) {
    dependsOn createContainerAeroSpike
    targetContainerId createContainerAeroSpike.getContainerId()
}
task start_presidioAnalyzer(type: DockerStartContainer) {
    dependsOn createContainerPresidioAnalyzer
    targetContainerId createContainerPresidioAnalyzer.getContainerId()
}
task start_presidioAnonymizer(type: DockerStartContainer) {
    dependsOn createContainerPresidioAnonymizer
    targetContainerId createContainerPresidioAnonymizer.getContainerId()
}


task wait_aerospike_startup {
    dependsOn start_aerospike
    doLast {
        timeout.set(Duration.ofMinutes(3))
        logger.info("Waiting for AeroSpike instance on localhost:3000")
        while (!get_port_status(3000))
            sleep(5 * 1000)
    }
}
task wait_presidio_startup {
    dependsOn start_presidioAnalyzer
    dependsOn start_presidioAnonymizer
    doLast {
        timeout.set(Duration.ofMinutes(3))
        logger.info("Waiting for Presidio instance on localhost:5001")
        while (!get_port_status(5001))
            sleep(5 * 1000)
    }
}

task stop_aerospike(type: DockerStopContainer) {
    targetContainerId createContainerAeroSpike.getContainerId()
}
task stop_presidio_analyzer(type: DockerStopContainer) {
    targetContainerId createContainerPresidioAnalyzer.getContainerId()
}
task stop_presidio_anonymizer(type: DockerStopContainer) {
    targetContainerId createContainerPresidioAnonymizer.getContainerId()
}


task check_if_aerospike_running {
    def is_aerospike_present = get_port_status(3000)
    if (is_aerospike_present) {
        logger.warn("Using existing AeroSpike instance running on localhost:3000")
    } else {
        logger.warn("Starting an instance of AeroSpike on localhost:3000")
        test.dependsOn wait_aerospike_startup
        test.finalizedBy stop_presidio_anonymizer
    }
}

task check_if_presidio_running {
    def is_presidio_present = get_port_status(5001)
    if (is_presidio_present) {
        logger.warn("Using existing Presidio instance running on localhost:5001")
    } else {
        logger.warn("Starting an instance of Presidio Analyzer & Anonymizer on ports 5001 and 5002")
        test.dependsOn wait_presidio_startup
        test.finalizedBy stop_presidio_analyzer
        test.finalizedBy stop_presidio_anonymizer
    }
}
test.dependsOn check_if_aerospike_running
test.dependsOn check_if_presidio_running


/**************************************************************************************************************
     Produce a FatJar for Testing and Deployment purposes
/**************************************************************************************************************/
import java.time.ZoneId
import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

mainClassName = 'com.ceridian.search.main.SearchAnonymizerAPI'
shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
    manifest {
        attributes 'Implementation-Title': rootProject.name
        attributes 'Implementation-Version': rootProject.version
        attributes 'Implementation-Vendor-Id': rootProject.group
        attributes 'Build-Time': ZonedDateTime.now(ZoneId.of("UTC")).format(DateTimeFormatter.ISO_ZONED_DATE_TIME)
        attributes 'Built-By': InetAddress.localHost.hostName
        attributes 'Created-By': 'Gradle ' + gradle.gradleVersion
        attributes 'Main-Class': mainClassName
    }
}

runShadow {
    args = ['server', "${project.rootDir}/config.yml"]
}
